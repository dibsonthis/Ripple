import [readf] : io
import string

extend String {
	substring: (start: Number, end: Number): String => {
		var substr = ""
		const str = self
		for (start..end, index, value) {
			substr += str[value]
		}
		ret substr
	},
	startsWith: (str: String): Boolean => self.substring(0, str.length) == str,
	endsWith: (str: String): Boolean => self.substring(self.length-str.length, self.length) == str
}

var data: [[String]] = 
	readf("/Users/adib/Dev/Personal/Vortex/playground/data/people.csv")
	.split("\n")
	.map((row: String): [String] => row.split(","))

println(data)

const headers: [String] = data[0]

data.remove_at(0)

type DataObj {
	id: 		Number = 0, 
	firstname: 	String = "", 
	lastname:  	String = "",
	email: 		String = "", 
	date: 		String = "",
	profession: String = ""
}

var csvList: [DataObj] = []

for (data, rowI, row) {
	var rowValue: DataObj = DataObj {};
	for (row, rowValueI, rowVal) {
		if (headers[rowValueI] == "id") {
			rowValue[headers[rowValueI]] = number(rowVal)
		} else {
			rowValue[headers[rowValueI]] = rowVal
		}
	}
	csvList.append(rowValue)
}

const selectedPeople: [String] 
	= csvList
	.filter((elem: DataObj): Boolean => elem.id >= 500 && elem.id <= 550 && elem.firstname.startsWith("Al"))
	.map((elem: DataObj): String => f"${elem.firstname} ${elem.lastname} (${elem.email}): ${elem.id}")

for (selectedPeople, i, person) {
	println(person)
}